assert(defined(mds_build_dir))

template("pkggroup") {
  group(target_name) {
    not_needed(invoker, "*")

    input_list = string_split(target_name, "/")

    input_len = 0
    foreach(in, input_list) {
      input_len += 1
    }

    pkg_target_dir = exec_script("${mds_build_dir}/scripts/package.py",
                                 [ input_list[0] ],
                                 "string")

    public_deps = []

    input_path = "${pkg_target_dir}"
    input_idx = 0
    foreach(in, input_list) {
      if (input_idx > 0) {
        input_path += "/" + in
      }
      input_idx += 1
    }

    if (defined(invoker.deps)) {
      foreach(d, invoker.deps) {
        public_deps += [ input_path + "/" + d ]
      }
    } else {
      input_idx = 0
      foreach(in, input_list) {
        if (input_idx == input_len - 1) {
          public_deps += [ input_path + "/:" + in ]
        }
        input_idx += 1
      }
    }
  }
}
