declare_args() {
  systemview_rtt_buffer_size = 2048
  systemview_use_static_buffer = false
  systemview_post_mortem_mode = false
}

declare_args() {
  systemview_pkg = "utils/systemview"
  systemview_ver = "V354"
  systemview_url =
      "https://www.segger.com/downloads/systemview/SystemView_Src_{version}.zip"
}

declare_args() {
  if (!defined(systemview_dir)) {
    systemview_ret = exec_script("${mds_script_dir}/package.py",
                                 [
                                   "${systemview_pkg}",
                                   "-u=${systemview_url}",
                                   "-v=${systemview_ver}",
                                   "-d=${mds_download_dir}",
                                   "-p=${mds_package_dir}",
                                 ],
                                 "string")
  }
}

declare_args() {
  if (!defined(systemview_dir)) {
    print("package ${systemview_pkg}-${systemview_ret}")
    systemview_dir = "${mds_package_dir}/${systemview_pkg}/${systemview_ret}/"
  }
}

config("systemview_config") {
  include_dirs = [
    "${systemview_dir}/SEGGER",
    "${systemview_dir}/Config",
  ]

  defines = [
    "SEGGER_RTT_SECTION=\".segger.rtt\"",
    "SEGGER_SYSVIEW_SECTION=\".segger.sysview\"",
    "SEGGER_SYSVIEW_RTT_BUFFER_SIZE=${systemview_rtt_buffer_size}",
  ]

  if (systemview_use_static_buffer) {
    defines += [ "SEGGER_SYSVIEW_USE_STATIC_BUFFER=1" ]
  } else {
    defines += [ "SEGGER_SYSVIEW_USE_STATIC_BUFFER=0" ]
  }

  if (systemview_post_mortem_mode) {
    defines += [ "SEGGER_SYSVIEW_POST_MORTEM_MODE=1" ]
  } else {
    defines += [ "SEGGER_SYSVIEW_POST_MORTEM_MODE=0" ]
  }
}

source_set("systemview") {
  sources = [
    "${systemview_dir}/SEGGER/SEGGER_RTT.c",
    "${systemview_dir}/SEGGER/SEGGER_SYSVIEW.c",
  ]

  defines = [ "RTT_USE_ASM=0" ]

  public_configs = [ ":systemview_config" ]
}
